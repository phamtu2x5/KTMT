.Model Large                  ;Khai bao kich thuoc bo nho su dung la large
.Stack 100H
.Data
    if_p1 db 0d               ;Kiem tra xem nguoi choi 1 da nhap ten chua
    if_p2 db 0d               ;Kiem tra xem nguoi choi 2 da nhap ten chua
    crlf  db 13,10,'$'
    player_pos    dw 1760d        ;Vi tri nguoi choi
    arrow_pos     dw 0d           ;Vi tri mui ten
    arrow_status  db 0d           ;0=San sang de ban mui ten moi, !0=Co mui ten dang bay
    arrow_limit   dw 22d          ;Gioi han bay cua mui ten
    balloon_pos   dw 3860d        ;Vi tri bong bay
    
    direction      db 0d          ;Huong di chuyen cua Player
    direction_up   equ 8          ;Quy dinh ma so 8 cho huong len
    direction_down equ 2          ;Quy dinh ma so 2 cho huong xuong
    
    high_score_header db 10,13,' ',10,13,'                              High Scores:',10,13,' ',10,13
                      db '                     Name          |   Score',10,13,'$' 

    score_line1 db '                     ',                    
    name1_buf   db 10 dup(' '),                         
                db '    |   ',                                           ;Bo dem dung de luu ten nguoi choi
    score1_buf  db 5 dup(' '),
                db 10, 13, '$'                                      ;Bo dem dung de luu diem

    score_line2 db '                     ',                     
    name2_buf   db 10 dup(' '),                                         
                db '    |   ',                                           ;Bo dem dung de luu ten nguoi choi
    score2_buf  db 5 dup(' '),
                db 10, 13, '$'                                      ;Bo dem dung de luu diem
    state      db 'iiiiiiiiiiiddddddddddddddddddd$',10,13,          ;Thanh trang thai dung de ghi ra highscore/hits/miss
    
    hits dw 0d                                                      ;Gia tri hit
    miss dw 0d                                                      ;Gia tri miss
    highScore dw 0d                                                 ;Diem cao nhat
    greatestScoreOfPlayer1 dw 0d                                    ;Diem cao nhat cua Player1
    greatestScoreOfPlayer2 dw 0d                                    ;Diem cao nhat cua Player2
    currentScoreOfPlayer1  dw 0d                                    ;Diem hien tai cua Player1
    currentScoreOfPlayer2  dw 0d                                    ;Diem hien tai cua Player2
    
    game_over_board dw '  ',10,13
    dw ' ',10,13
    dw    '          --------------------------------------------------------      ',10,13
    dw    '          ########################################################      ',10,13
    dw ' ',10,13
    dw ' ',10,13
    dw ' ',10,13
    dw '                                Game Over',10,13
    dw '                        Press Enter to start again',10,13
    dw ' ',10,13
    dw ' ',10,13
    dw '                        For 1st player press ( 1 ) ',10,13
    dw '                        For 2nd player press ( 2 ) ',10,13
    dw '                     For retry with current player press ( enter ) $',10,13             ; Man hinh gameover
      
    TB_BATDAU dw '                                                                     ',10,13,
    dw '                                                                     ',10,13,
    dw '                                                                     ',10,13,
    dw '                                                                     ',10,13,
    dw '              *****************************************************',10,13, 
    dw '              *                                                   *',10,13,
    dw '              *                                                   *',10,13,
    dw '              *              BALLOON SHOOTING GAME                *',10,13,
    dw '              *                                                   *',10,13,
    dw '              *                                                   *',10,13,
    dw '              *      This game is made by Team 05 - Group 10      *',10,13,
    dw '              *                                                   *',10,13,
    dw '              *          COMPUTER ARCHITECTURE - D23PTIT          *',10,13,
    dw '              *                                                   *',10,13,
    dw '              *              Teacher: Tran Tien Cong              *',10,13,
    dw '              *                                                   *',10,13,
    dw '              *      --------------------------------------       *',10,13,
    dw '              *        Use up and down key to move player         *',10,13,
    dw '              *           and space button to shoot               *',10,13,
    dw '              *      --------------------------------------       *',10,13,
    dw "              *  Take Care You only get 8 misses then you're out  *",10,13,
    dw '              *                                                   *',10,13,
    dw '              *                                                   *',10,13,
    dw '              *          >>>>Press Enter to start<<<<             *',10,13,
    dw '              *                                                   *',10,13,
    dw '              *                                                   *',10,13,
    dw '              *****************************************************',10,13,'$'          ;Man hinh game start
    
    msg db 10, 13, " UserName: $"      ;Thong bao nhap ten nguoi choi                     
    
    name_player1 db 11 dup(?)          ;Ten Player1 1
    len_player1  db ?                  ;Do dai ten Player1
    name_player2 db 11 dup(?)          ;Ten Player 2   
    len_player2  db ?                  ;Do dai ten Player2
    
    times        db 0d                 ;Bien luu tru xem dang la luot choi cua Player1 hay Player2
       
.Code
Main Proc
    mov AX,@Data
    mov  DS,AX
    mov  AX,0B800H                    
    mov  ES,AX                         ;Sao chep dia chi segment bat dau cua bo nho dem video vao thanh ghi ES
    
start:
    lea dx,TB_BATDAU
    mov AH,9
    INT 21H                            ;In ra man hinh thong bao bat dau game
input:
    mov ah,1                           ;Nhap Index nguoi choi
    int 21h
    cmp al,'1'               
    je Display_name1                   ;Neu nhap '1' -> Nhay den thao tac nhap ten Player1
    cmp al,'2'              
    je clear_and_display2              ;Neu nhap '1' -> Nhay den thao tac nhap ten Player1
    cmp al,13d              
    je continue                        ;Neu nhan phim enter -> Nhay den continue
    cmp times,1
    je game_over                                                                    
    jne start                          ;Neu khong nhan cac phim hop le, quay tro lai man hinh bat dau

clear_and_display2:
    CALL Clear_screen      
    JMP Display_name2
        
continue:
    
    mov ah,0                           
    mov al,3                           ;Che do video 3 de xoa man hinh cu
    int 10h                            ;Xoa man hinh
    
    cmp times,0                        
    je display_name1                   ;Neu nhap Enter luc bat dau game, mac dinh la nguoi choi 1
    cmp times,1
    je show_name1                      ;Neu dang la nguoi choi 1 va bam Enter-> Choi lai
    jne show_name2                     ;Neu dang la nguoi choi 2 va bam Enter-> Choi lai
main_loop:

    mov ah,1                                        ;Chuc nang 1 cua INT 16H de kiem tra bo dem ban phim 
    int 16h
    
    jne key_pressed                                 ;Neu co phim duoc nhan -> Nhay den Key_pressed
    
    cmp miss,8
    jge game_over                                   ;Neu miss >=8 , Game Over
    
    mov dx,arrow_pos
    cmp dx,balloon_pos
    je hit                                          ;Neu mui ten va bong cham nhau, tang diem
    
    cmp direction,direction_up
    je Move_up                                      ;Di chuyen Player len neu nhan Up Key
    
    cmp direction,direction_down
    je Move_down                                    ;Di chuyen Player xuong neu nhan Down Key
    
    mov dx,arrow_limit
    cmp arrow_pos,dx
    jge hide_arrow                                  ;An mui ten di neu bay qua limit
    
    cmp balloon_pos,0d
    jle miss_balloon                                ;Neu bong bay qua man hinh, tang miss
    jne render_balloon                              ;Neu khong thi tiep tuc ve bong

HIT:
    MOV AH,2
    MOV DL,07h          ; Phat am thanh khi shoot trong balloon
                        ; DL = 07h (ascii beep) => phat ra tieng beep khi shoot trung bong bay
    INT 21H
    
    INC hits            ; Tang so luong hits len
    CMP times,1
    JE p1ShootCorrect   ; Tang diem cho nguoi choi 1 
    JNE p2ShootCorrect  ; Tang diem cho nguoi choi 2
    
Render_balloon:
    MOV CL,' '          ; Xoa bong cu
    MOV CH,1111B        ; CH dung de luu mau sac cua ky tu trong bo nho video
                        ; 1111 la mau trang (mau mac dinh cho man hinh van ban trong nhieu he thong)
    ;CX : byte cao phu trach ky tu in ra
    ;     byte thap phu trach cac thuoc tinh nhu : mau sac , kieu hien thi cua ky tu                        
    
    ;CH = 1111B (tuc 0Fh): nen den, chu trang
    ;CH = 0010B (tuc 02h): nen den, chu xanh la
    ;CH = 1001B (tuc 09h): nen den, chu xanh duong
    ;CH = 1100B (tuc 0Ch): nen den, chu do
    
    MOV BX,balloon_pos  ; Lay vi tri hien tai cua bong
    MOV ES:[BX],CX      ; Ghi khoang trong vao vi tri bong cu de xoa bong cu di
    
    CMP hits,2          ; Kiem tra so lan ban trung
    JE Speedup_2x       ; Neu ban trung 1 so lan quy dinh thi se tang toc do                            
    
    CMP hits,3          
    JE Speedup_2x
    JG Speedup_3x            ; Neu hits > 3 => Tang toc do bong len x3
    
    SUB balloon_pos,160d     ; Di chuyen bong len tren(Tuc la ve~ bong moi)
                             ; Di chuyen bong len 1 dong = 160 byte
    ; Moi dong trong che do 80x25 text mode VGA chiem 160 byte
    ; (80 ky tu x 2byte/ky tu = 160 byte)
                            
    ;Ve bong moi                           
    MOV CL,15d               ; Ma ky tu cho bong moi
    ; 15d trong ASCII mo rong : la bieu tuong mat cuoi hoac 1 dau tron
    MOV CH,0010B             ; Mau bog (mau xanh la cay)
    
    MOV BX,balloon_pos       ; Lay vi tri hien tai cua bong
    MOV ES:[BX],CX           ; Dat bong vua ve vao vi tri 
    
    CMP arrow_status,1      ; Kiem tra xem co mui ten dang ban khong
    ; arrow_status = 0 : mui ten san sang (chua duoc ban)
    ; arrow_status != 0: mui ten dang bay      
    JE Render_arrow          ; Neu co , xu ly ve mui ten
    JNE Inside_loop2         ; Neu khong , tiep tuc vong lap
    
Speedup_2x:
    SUB balloon_pos,320d    ; Tang toc do cua bong len gap doi
    ; 160x2 = 320 => nhay 2 dong trong 1 buoc
    
    MOV CL,15D              ; Ve bong
    MOV CH,0010B            ; Mau bong (mau xanh la cay)
    
    MOV BX,balloon_pos      ; Lay vi tri hien tai cua bong
    MOV ES:[BX],CX          ; Dat bong vua ve vao vi tri 
    
    CMP arrow_status,1d     ; Kiem tra xem co mui ten dang ban khong
                            ; arrow_status = 0 : mui ten san sang (chua duoc ban)
                            ; arrow_status != 0: mui ten dang bay
    JE Render_arrow         ; Neu co , xu ly goi label ve mui ten
    JNE Inside_loop2        ; Neu khong , tiep tuc vong lap
    
Speedup_3x:
    SUB balloon_pos,480d    ; Tang toc do cua bong len gap ba 
    
    MOV CL,15D              ; Ve bong
    MOV CH,0010B            ; Mau bong
    
    MOV BX,balloon_pos      ; Lay vi tri hien tai cua bong
    MOV ES:[BX],CX          ; Dat bong vua ve vao vi tri
    
    CMP arrow_status,1d     ; Kiem tra xem co mui ten dang ban khong
                            ; arrow_status = 0 : mui ten san sang (chua duoc ban)
                            ; arrow_status != 0: mui ten dang bay
    JE Render_arrow         ; Neu co , xu ly goi label ve mui ten
    JNE Inside_loop2        ; Neu khong , tiep tuc vong lap
    
Render_arrow:
    MOV CL,' '              ; Xoa vi tri cu
    MOV CH,1111B            ; Mau mac dinh (dung de xoa mui ten)
    
    MOV BX,arrow_pos
    MOV ES:[BX],CX
    
    ADD arrow_pos,4d        ; Ve mui ten moi
    MOV CL,26D              ; ma ASCII cho ky tu mui ten
    MOV CH,1001B            ; mau mui ten
    
    MOV BX,arrow_pos        ; Lay vi tri mui ten
    MOV ES:[BX],CX          ; Dat mui ten vua ve vao vi tri

Inside_loop2:
    MOV CL,125D             ; Ve nguoi choi (nguoi choi la ki tu '}' giong cai cung )
    MOV CH,1100B            ; mau sac player
    MOV BX,player_pos       ; Vi tri hien tai cua player
    MOV ES:[BX],CX          ; Chuyen player vua ve vao vi tri
    
    JMP MAIN_LOOP  

; Ham di chuyen len cua player
Move_up:
    MOV CL,' '              ; Xoa vi tri cu
    MOV CH,1111B            ; Mau player
    
    MOV BX,player_pos       ; Lay vi tri cu
    MOV ES:[BX],CX          ; Xoa player bang cach chen mau mac dinh vao khoang trong vua tao 
    
    SUB player_pos,160d     ; Di chuyen len 1 dong = 160 byte
    MOV direction,0         ; reset direction ve 0 de tranh viec di chuyen lien tuc ma khong can nhan phim
    
    JMP MAIN_LOOP
    
Move_down:
    MOV CL,' '              ; Xoa vi tri cu
    MOV CH,1111B            ; Mau player
    MOV BX,player_pos       ; Lay vi tri cu
    MOV ES:[BX],CX          ; Xoa player bang cach chen mau mac dinh vao khoang trong vua tao 
    
    ADD player_pos,160d     ; Di chuyen xuong 1 dong = 160 byte
    MOV direction,0         ; reset direction ve 0 de tranh viec di chuyen lien tuc ma khong can nhan phim
    
    JMP MAIN_LOOP   
key_pressed:
   mov ah, 0                        ;Cho nhan phim
   int 16h
   
   cmp ah, 39h                      ;Nhan phim Space
   je spaceKey 
   
   cmp ah, 48h                      ;Nhan phim UpKey
   je upKey
   
   cmp ah, 50h                      ;Nhan phim DownKey
   je downKey
   
   cmp ah, 4Bh                      ;Nhan phim LeftKey
   je leftKey
   
   cmp ah, 4Dh                      ;Nhan phim RightKey
   je rightKey
   
   jmp main_loop
   
spaceKey:                           ;Shoot
   cmp arrow_status, 0              ;Neu mui ten san sang thi ban
   je shoot
   jmp main_loop
   
upKey:                              ;Di chuyen player len
   mov direction, 8
   jmp main_loop
   
downKey:                            ;Di chuyen player xuong
   mov direction, 2
   jmp main_loop
   
rightKey:                           ;Hit++  (debug)
   mov cl, ' '   
   mov bx, balloon_pos
   mov es:[bx], cx                  ;Xoa bong cu             
   jmp hit                          ;Nhay den ham hit de tang hits
   
leftKey:                            ;Miss++ (debug)
   mov cl, ' '
   mov bx, balloon_pos
   mov es:[bx], cx                  ;Xoa bong cu
   jmp miss_balloon                 ;Nhay den ham miss_balloon de tang miss
   
shoot:
   mov dx, player_pos
   mov arrow_pos, dx                ;Dat mui ten o vi tri player
   
   mov dx, player_pos
   mov arrow_limit, dx 
   add arrow_limit, 22d             ;Dat gioi han mui ten cach player 22d
   
   mov arrow_status, 1d             ;Mui ten dang bay tranh spam ban  
   jmp main_loop
   
miss_balloon:
   add miss, 1                      ;Tang miss them 1
   call show_score                  
   lea dx, state
   mov ah, 9
   int 21h                          ;Hien thi xau highscore/hit/miss
   mov ah, 2
   mov dl, 13                       ;Xuong dong de tranh in de
   int 21h
   jmp create_balloon 
   
create_balloon:
   mov balloon_pos, 3860d           ;Tao lai vi tri bong o vi tri ban dau
   jmp render_balloon               ;Nhay den ham ve bong di chuyen
   
hide_arrow:
   mov arrow_status, 0              ;Mui ten chua duoc ban
   
   mov cl, ' '                      
   mov bx, arrow_pos
   mov es:[bx], cx                  ;Xoa mui ten
   
   cmp balloon_pos, 0d              ;Kiem tra vi tri cua bong
   jle miss_balloon                 ;If bong khong con trong man hinh thi nhay den miss
   jmp render_balloon               ;Else tiep tuc ve bong
   
game_over:
    call clear_screen                   ;Xoa man hinh
    mov currentScoreOfPlayer1, 0        ;Reset diem
    mov currentScoreOfPlayer2, 0        ;Reset diem

    call score_list                     ;Goi ham in danh sach diem

    mov ah, 9
    lea dx, high_score_header
    int 21h                             ;In ra dong chu Highscore

    mov ah, 9
    lea dx, score_line1                 
    int 21h                             ;In ra diem nguoi choi cao nhat 

    
    mov ah, 9
    lea dx, score_line2 
    int 21h                             ;In ra diem nguoi choi cao thu hai

    
    mov ah, 9
    mov dx, offset game_over_board      
    int 21h                             ;In ra man hinh ket thuc game

    mov hits, 0                         ;Reset hits
    mov miss, 0                         ;Reset miss
    mov player_pos, 1760d               ;Reset vi tri Player
   

    jmp input                           ;Goi thao tac nhap xuat de bat dau choi lai 

;Cap nhat diem cao nhat cua Player1   
update_highScore1:                                                                
        mov greatestScoreOfPlayer1, ax  ; Luu diem cao nhat cua nguoi choi 1
        mov highScore, ax                ; Cap nhat diem cao hien tai
            
        call show_score                   ; Goi ham hien thi diem so
        mov ah, 9                        ; Chuan bi hien thi chuoi
        lea dx, state                    ; Lay dia chi cua bien state
        int 21h                          
        mov ah, 2                        ; Chuan bi in ky tu
        mov dl, 0dh                      ; Ky tu xuong dong
        int 21h                          
        jmp create_balloon               ; Quay ve tao bong bay

;Cap nhat diem cao nhat cua Player2              
update_highScore2:                                                                 
        mov greatestScoreOfPlayer2, ax  ; Luu diem cao nhat cua nguoi choi 2
        mov highScore, ax                ; Cap nhat diem cao hien tai
            
        call show_score                   ; Goi ham hien thi diem so
        mov ah, 9                        ; Chuan bi hien thi chuoi
        lea dx, state                    ; Lay dia chi cua bien state 
        int 21h                          
        mov ah, 2                        ; Chuan bi in ky tu
        mov dl, 0dh                      ; Ky tu xuong dong
        int 21h                          
        jmp create_balloon               ; Quay ve tao bong bay    

show_name1:
    call clear_screen                    ; Xoa man hinh
    mov ax, @data
    mov ds, ax
    lea dx, msg                         
    mov ah, 9
    int 21h                              ; In ra dong chu UserName

   
    lea dx, name_player1
    mov ah, 9
    int 21h                              ; In ten player1

    mov ah, 9
    lea dx, crlf
    int 21h

    call show_score                       ; Goi thao tac xu ly state de in diem
    lea dx, state
    mov ah, 9
    int 21h                               ; In highscore/hits/miss

    mov ah, 2
    mov dl, 0dh
    int 21h                               ; In xuong dong

    mov times, 1                          ; Danh dau dang la luot choi cua Player1
    jmp main_loop
ret
                                          ; Quay ve vong lap chinh
       
show_name2: 
    call clear_screen                     ; Clear screen
    mov ax, @data
    mov ds, ax
    lea dx, msg             
    mov ah, 9
    int 21h                               ; In ra dong UserName

  
    lea dx, name_player2   
    mov ah, 9
    int 21h                               ; In ra ten Player2

    mov ah, 9
    lea dx, crlf
    int 21h                               ; In xuong dong

    call show_score                       ; Goi thao tac xu ly state de in diem
    lea dx, state
    mov ah, 9
    int 21h                               ; In highscore/hits/miss

    mov ah, 2
    mov dl, 0dh
    int 21h                               ; In xuong dong

    mov times, 2                          ; Danh dau dang la luot choi cua Player2
    jmp main_loop                         ; Quay ve vong lap chinh
ret ;

p1shootCorrect:                                                               
       inc currentScoreOfPlayer1        ; Tang diem cho nguoi choi 1
       mov dx, greatestScoreOfPlayer1   ; Lay diem cao nhat cua nguoi choi 1
       mov cx, currentScoreOfPlayer1    ; Lay diem hien tai
       cmp cx, dx                       ; So sanh diem hien tai vs diem cao nhat cua nguoi choi 1
       jg updateGreatestScoreOfPlayer1  ; Neu diem cao hon, cap nhat diem cao nh?t                                                 
            
       mov bx, highScore                ; Lay diem cao global hien tai
       mov ax, greatestScoreOfPlayer1   ; Lay diem cao nhat
       cmp ax, bx                       ; So sanh diem cao nhat vs diem cao hien tai
       jg update_highScore1             ; Neu diem cao nhat lon hon, cap nhat diem cao 
       
       call show_score                  ; Goi ham hien thi diem so
       lea dx, state                    ; Lay dia chi cua bien state
       mov ah, 9                        ; Chuan bi hien thi chuoi
       int 21h                          ; Goi ngat 21h - hien thi chuoi
       mov ah, 2                        ; Chuan bi in ky tu
       mov dl, 0dh                      ; Ky tu xuong dong                                                                                                                                                             
       int 21h                          ; Goi ngat 21h - in ky tu    
       jmp create_balloon               ; Quay ve tao bong bay                                              

p2shootCorrect:                                                                 
       inc currentScoreOfPlayer2        ; Tang diem cho nguoi choi 2
       mov dx, greatestScoreOfPlayer2   ; Lay diem cao nhat cua nguoi choi 2
       mov cx, currentScoreOfPlayer2    ; Lay diem hien tai
       cmp cx, dx                       ; So sanh diem hien tai vs diem cao nhat cua nguoi choi 2
       jg updateGreatestScoreOfPlayer2  ; Neu diem cao hon, cap nhat diem cao nh?t                                                 

       mov bx, highScore                ; Lay diem cao hien tai
       mov ax, greatestScoreOfPlayer2   ; Lay diem cao nhat
       cmp ax, bx                       ; So sanh diem cao nhat vs diem cao hien tai
       jg update_highScore2             ; Neu diem cao nhat lon hon, cap nhat diem cao 
       
       call show_score                  ; Goi ham hien thi diem so                                                  
       lea dx, state                    ; Lay dia chi cua bien state
       mov ah, 9                        ; Chuan bi hien thi chuoi
       int 21h                          ; Goi ngat 21h - hien thi chuoi
            
       mov ah, 2                        ; Chuan bi in ky tu
       mov dl, 0dh                      ; Ky tu xuong dong
       int 21h                          ; Goi ngat 21h - in ky tu    
       jmp create_balloon               ; Quay ve tao bong bay                                           

updateGreatestScoreOfPlayer1:                                                            
         mov dx, cx                     ; Luu diem hien tai vao dx
         mov greatestScoreOfPlayer1, dx  ; Cap nhat diem cao nhat cua nguoi choi 1
         mov bx, highScore               ; Lay diem cao hien tai
         mov ax, dx                      ; Lay diem cao nhat
         cmp ax, bx                      ; So sanh diem cao nhat vs diem cao hien tai
         jg update_highScore1            ; Neu diem cao nhat lon hon, cap nhat diem cao 
       
         call show_score                  ; Goi ham hien thi diem so                                                 
         lea dx, state                   ; Lay dia chi cua bien state
         mov ah, 9                       ; Chuan bi hien thi chuoi
         int 21h                         ; Goi ngat 21h - hien thi chuoi
         mov ah, 2                       ; Chuan bi in ky tu
         mov dl, 0dh                     ; Ky tu xuong dong
         int 21h                         ; Goi ngat 21h - in ky tu    
         jmp create_balloon              ; Quay ve tao bong bay                                          

updateGreatestScoreOfPlayer2:                                                             
       mov dx, cx                       ; Luu diem hien tai vao dx
       mov greatestScoreOfPlayer2, dx   ; Cap nhat diem cao nhat cua nguoi choi 2
       mov bx, highScore                 ; Lay diem cao hien tai
       mov ax, dx                       ; Lay diem cao nhat
       cmp ax, bx                       ; So sanh diem cao nhat vs diem cao hien tai
       jg update_highScore2             ; Neu diem cao nhat lon hon, cap nhat diem cao 
       
       call show_score                  ; Goi ham hien thi diem so                                                  
       lea dx, state                    ; Lay dia chi cua bien state
       mov ah, 9                        ; Chuan bi hien thi chuoi
       int 21h                          ; Goi ngat 21h - hien thi chuoi
       mov ah, 2                        ; Chuan bi in ky tu
       mov dl, 0dh                      ; Ky tu xuong dong
       int 21h                          ; Goi ngat 21h - in ky tu    
       jmp create_balloon               ; Quay ve tao bong bay                                          

main endp

show_score proc                                                        
    lea bx,state                      ; Lay dia chi cua bien state
    mov cx, highScore                 ; Lay diem cao hien tai
    add cx,48d                        ; Chuyen sang ky tu
    mov [bx],    9d                   ; Thong tin diem so
    mov [bx+1],  9d                   ; Ky tu xuong dong
    mov [bx+2],  9d                   ; Ky tu xuong dong
    mov [bx+3],  'H'                  ; Ky tu 'H'
    mov [bx+4],  'i'                  ; Ky tu 'i'                                        
    mov [bx+5],  'g'                  ; Ky tu 'g'
    mov [bx+6],  'h'                  ; Ky tu 'h'
    mov [bx+7],  ' '                  ; Khoang trang
    mov [bx+8],  'S'                  ; Ky tu 'S'
    mov [bx+9],  'c'                  ; Ky tu 'c'
    mov [bx+10], 'o'                  ; Ky tu 'o'
    mov [bx+11], 'r'                  ; Ky tu 'r'
    mov [bx+12], 'e'                  ; Ky tu 'e'
    mov [bx+13], ':'                  ; Ky tu ':'
    mov [bx+14], cx                   ; Diem cao hien tai
    mov dx, hits                      ; Lay so lan da cham
    add dx,48d                        ; Chuyen sang ky tu 
    mov [bx+15], 9d                   ; Ky tu xuong dong
    mov [bx+16], 'H'                  ; Ky tu 'H'
    mov [bx+17], 'i'                  ; Ky tu 'i'                                        
    mov [bx+18], 't'                  ; Ky tu 't'
    mov [bx+19], 's'                  ; Ky tu 's'
    mov [bx+20], ':'                  ; Ky tu ':'
    mov [bx+21], dx                   ; So lan da cham
    mov dx, miss                      ; Lay so lan bo qua
    add dx,48d                        ; Chuyen sang ky tu                                                       
    mov [bx+22], ' '                  ; Khoang trang
    mov [bx+23], 'M'                  ; Ky tu 'M'
    mov [bx+24], 'i'                  ; Ky tu 'i'
    mov [bx+25], 's'                  ; Ky tu 's'
    mov [bx+26], 's'                  ; Ky tu 's'
    mov [bx+27], ':'                  ; Ky tu ':'
    mov [bx+28], dx                   ; So lan bo qua
ret    
show_score endp 

clear_screen proc near
        mov ah,0                        ; Chuan bi cho ngat 10h
        mov al,3                        ; Chon che do man hinh
        int 10h                         ; Goi ngat 10h - xoa man hinh
ret
clear_screen endp 

display_name1 proc
    cmp if_p1, 0
    jne show_name1                      ; Neu nguoi choi 1 da nhap ten, In ra ten 

    mov times, 1                        ; Danh dau la luot choi cua Player1

   
    mov ax, @data                       
    mov ds, ax
    lea dx, msg                         
    mov ah, 9
    int 21h                              ; In ra chuoi Username

    mov si, offset name_player1
    mov byte ptr [si], '$'               ; Khoi tao bo dem name_player1 ban dau la 1 chuoi rong
    mov bl, 0                            ; Dat bien dem so luong ky tu ban dau bl=0

input_loop_p1:                           ; Vong lap nhap tung ky tu
    mov ah, 1                   
    int 21h                                 

    cmp al, 0Dh                       
    je end_input_p1                      ; Neu phim Enter duoc nhap vao thi ket thuc nhap ten

   
    cmp bl, 9                               
    jge input_loop_p1                    ; Bo qua cac ky tu thua, gioi han do dai toi da cua ten la 9 ky tu

    mov [si], al                         ; Luu ky tu vua nhap vao name_player1
    inc si                               ; Tang si
    inc bl                               ; Tang so luong ky tu da nhap
    mov byte ptr [si], '$'               ; Dam bao ten da nhap luon co ky tu '$' o cuoi ten

    jmp input_loop_p1                    ; Tiep tuc nhap ten

end_input_p1:                            ; Ket thuc vong lap nhap ten
    mov len_player1, bl                  ; Luu do dai ten Player1
 
    call clear_screen                    ; Xoa man hinh
    mov ax, @data
    mov ds, ax
    lea dx, msg            
    mov ah,9
    int 21h
    lea dx, name_player1
    int 21h                               ; In ra ten Player1

    mov ah, 9
    lea dx, crlf
    int 21h                               ; In xuong dong

    call show_score                       ; Xu li diem
    lea dx, state
    mov ah, 9
    int 21h                               ; In ra dong "Highscore/Hits/Miss"

    mov ah, 2
    mov dl, 0dh
    int 21h                               ; In xuong dong

    inc if_p1                             ; Danh dau Player1 da nhap ten
    jmp main_loop
ret
display_name1 endp

display_name2 proc
    cmp if_p2, 0
    jne show_name2          

    mov times, 2           

   
    mov ax, @data
    mov ds, ax
    lea dx, msg
    mov ah, 9
    int 21h                

    mov si, offset name_player2 
    mov byte ptr [si], '$' 
    mov bl, 0             

input_loop_p2:             
    mov ah, 1               
    int 21h

    cmp al, 0Dh            
    je end_input_p2        

   
    cmp bl, 9
    jge input_loop_p2      

    mov [si], al          
    inc si                  
    inc bl                 
    mov byte ptr [si], '$' 

    jmp input_loop_p2      

end_input_p2:             
    mov len_player2, bl    


    call clear_screen    
    mov ax, @data
    mov ds, ax
    mov ah,9
    lea dx,msg
    int 21h
    lea dx, name_player2   
    int 21h

    mov ah, 9
    lea dx, crlf
    int 21h

    call show_score
    lea dx, state
    mov ah, 9
    int 21h

    mov ah, 2
    mov dl, 0dh
    int 21h

    inc if_p2           
    jmp main_loop
ret
display_name2 endp

; Tao bang diem da xep hang, in ra ten 2 nguoi choi theo thu tu diem giam dan
score_list proc
    ; --- Xoa buffer name1_buf ---
    push di                                     ; Dua di vao stack de giu lai gia tri cu
    mov al, ' '
    mov cx, 10
    lea di, name1_buf 
clear_name1_loop:                               ; Vong lap reset name1_buf thanh 1 xau gom 10 ky tu ' '
    mov [di], al
    inc di
    loop clear_name1_loop
    pop di                                      ; Tra lai gia tri cua di ban dau

    ; --- Xoa buffer name2_buf ---              ; Tuong tu nhung la reset name2_buf
    push di                         
    mov al, ' '
    mov cx, 10
    lea di, name2_buf
clear_name2_loop:
    mov [di], al
    inc di
    loop clear_name2_loop
    pop di

    ; --- Xoa buffer score1_buf ---             ; Vong lap reset score1_buf thanh 1 xau gom 5 ky tu ' '
    push di
    mov al, ' '
    mov cx, 5
    lea di, score1_buf
clear_score1_loop:
    mov al, ' '
    mov [di], al
    inc di
    loop clear_score1_loop
    pop di

    ; --- Xoa buffer score2_buf ---             ; Vong lap reset score2_buf thanh 1 xau gom 5 ky tu ' '
    push di
    mov al, ' '
    mov cx, 5
    lea di, score2_buf
clear_score2_loop:
    mov al, ' '
    mov [di], al
    inc di
    loop clear_score2_loop
    pop di

    ; --- So sanh diem P1 va P2 ---
    mov cx, greatestScoreOfPlayer1
    mov ax, greatestScoreOfPlayer2
    cmp cx, ax
    jge P1_wins_or_tie          ; Neu P1 >= P2, nhay den P1_wins_or_tie

; --- Truong hop P2 > P1 ---
P2_is_higher:
    ; Xu ly dong 1: Player 2
    lea si, name_player2        ; Nguon ten P2
    lea di, name1_buf           ; Dich ten dong 1
    mov cx, 10                  ; So luong ky tu toi da
    call Internal_Copy_Name     ; Goi ham copy ten
    mov ax, greatestScoreOfPlayer2 ; Lay diem P2
    lea di, score1_buf          ; Dich diem dong 1
    call Internal_Format_Score  ; Goi ham dinh dang diem

    ; Xu ly dong 2: Player 1
    lea si, name_player1        ; Nguon ten P1
    lea di, name2_buf           ; Dich ten dong 2
    mov cx, 10                  ; So luong ky tu toi da
    call Internal_Copy_Name     ; Goi ham copy ten
    mov ax, greatestScoreOfPlayer1 ; Lay diem P1
    lea di, score2_buf          ; Dich diem dong 2
    call Internal_Format_Score  ; Goi ham dinh dang diem

    jmp score_list_finished     ; Nhay den ket thuc

; --- Truong hop P1 >= P2 ---
P1_wins_or_tie:
    ; Xu ly dong 1: Player 1
    lea si, name_player1        ; Nguon ten P1
    lea di, name1_buf           ; Dich ten dong 1
    mov cx, 10                  ; So luong ky tu toi da
    call Internal_Copy_Name     ; Goi ham copy ten
    mov ax, greatestScoreOfPlayer1 ; Lay diem P1
    lea di, score1_buf          ; Dich diem dong 1
    call Internal_Format_Score  ; Goi ham dinh dang diem

    ; Xu ly dong 2: Player 2
    lea si, name_player2        ; Nguon ten P2
    lea di, name2_buf           ; Dich ten dong 2
    mov cx, 10                  ; So luong ky tu toi da
    call Internal_Copy_Name     ; Goi ham copy ten
    mov ax, greatestScoreOfPlayer2 ; Lay diem P2
    lea di, score2_buf          ; Dich diem dong 2
    call Internal_Format_Score  ; Goi ham dinh dang diem

    jmp score_list_finished     ; Nhay den ket thuc

; --- Chuong trinh con cuc bo: Copy ten ---
Internal_Copy_Name:
    push ax                     ; Luu ax vi dung al
.copy_loop:
    cmp cx, 0                   ; 
    je .copy_done               ; Neu cx=0 thi nghia la da copy xong
    mov al, [si]                ; Lay ky tu nguon tu name_player
    cmp al, '$'                  
    je .copy_done               ; Neu la ky tu ket thuc thi dung thao tac Copy
    mov [di], al                ; Copy sang dich (name_buf)
    inc si                      ; Tang con tro nguon
    inc di                      ; Tang con tro dich
    dec cx                      ; Giam bien dem
    jmp .copy_loop              ; Lap lai
.copy_done:
    pop ax                      ; Phuc hoi ax
    ret                         ; Quay ve

; --- Chuong trinh con cuc bo: Dinh dang diem (0-99) ---
Internal_Format_Score:
    push bx                     ; Luu cac thanh ghi
    push cx
    push dx
    push si

    xor dx, dx                  ; Xoa dx truoc khi chia
    mov bx, 10                  ; So chia
    div bx                      ; Chia ax cho 10 => AX=Thuong(chuc), DX=Du(don vi)
    mov cx, ax                  ; Luu hang chuc vao cx

    mov ax, dx                  ; Lay hang don vi vao ax
    add al, '0'                 ; Chuyen don vi sang ASCII
    mov [di+1], al              ; Ghi vao vi tri thu 2 cua buffer diem

    mov ax, cx                  ; Lay lai hang chuc vao ax
    cmp al, 0                   ; Kiem tra hang chuc co bang 0 ko
    jne .tens_not_zero          ; Khong bang 0 thi nhay
    mov byte ptr [di], ' '      ; Neu bang 0 thi ghi space vao vi tri dau
    jmp .format_done            ; Nhay den ket thuc format
.tens_not_zero:
    add al, '0'                 ; Chuyen hang chuc sang ASCII
    mov [di], al                ; Ghi hang chuc vao vi tri dau
.format_done:
    pop si                      ; Phuc hoi cac thanh ghi
    pop dx
    pop cx
    pop bx
    ret                         ; Quay ve

score_list_finished:
    ret                         ; Ket thuc score_list
score_list endp

end main

   
         